import java.nio.file.Paths

plugins {
    id "com.google.protobuf" version "0.9.3"
    id "java"
    id "idea"
    id "application"
}

group 'me.nicholasnadeau'
version '0.1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

// define gRPC version
def GRPC_VERSION = '1.45.0'

dependencies {
    // https://mvnrepository.com/artifact/org.apache.commons/commons-math3
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

    // https://mvnrepository.com/artifact/io.grpc/grpc-netty
    implementation group: 'io.grpc', name: 'grpc-netty', version: GRPC_VERSION

    // https://mvnrepository.com/artifact/io.grpc/grpc-protobuf
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: GRPC_VERSION

    // https://mvnrepository.com/artifact/io.grpc/grpc-stub
    implementation group: 'io.grpc', name: 'grpc-stub', version: GRPC_VERSION

    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    // FARO lib
    implementation fileTree(dir: 'faro-lib', include: '*.jar')
}

// compile proto
protobuf {
    protoc {
        // https://mvnrepository.com/artifact/com.google.protobuf/protoc
        artifact = 'com.google.protobuf:protoc:3.19.4'
    }

    //noinspection GroovyAssignabilityCheck
    plugins {
        grpc {
            // https://mvnrepository.com/artifact/io.grpc/protoc-gen-grpc-java
            artifact = "io.grpc:protoc-gen-grpc-java:$GRPC_VERSION"
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// inform idea where the generated protos are
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

clean {
    //noinspection GroovyAssignabilityCheck
    delete protobuf.generatedFilesBaseDir
}

application {
    mainClass = 'me.nicholasnadeau.farotracker.RunService'
}
